TopLevel
    = Document
    ;

Document
    = Misc* DocType Misc* 
    ;
    
DocType
    = '<!DOCTYPE' _* <<DocTypeName@ '[' DTD* #doctype >> ']>'
    ;

DTD
    = _*  '<!' (Element@/Entity@/Attlist@) '>' _*
    ;

Element
	= _* 'ELEMENT' _* <<ElementName@ _* '(' Model (ValueSeparator Model)* ')' #element>> _*
	;

Attlist
	= _* 'ATTLIST' _* <<ElementName@ _* (AttParameter@)* #attlist>> _*
	;
	
Entity
	= _* 'ENTITY' _* <<EntityName@ _* String@ #entity>> _*
	;


//for Element
Model
	= _* (Member@) _*
	;
Member
	= << (Data@/MemberName@) (ValueSeparator (Data@/MemberName@))* #member>> 
	;
Data
	= << ( '#PCDATA' / 'EMPTY' / 'ANY' ) #data>>
	;

//for Attlist
AttParameter
	= << _* AttName@ _* DataType@ _* Default@ _* #attParameter>>
	; 
DataType
	= << ('CDATA'/'ID'/'IDREF'/'ENTITY'/'ENTITIES'/'NMTOKEN'/'NMTOKENS'/'Enumerated') #dataType>>
	;
Default
	= << DefaultType@  _* (DefaultValue@)* #default>>
	;
	
DefaultType
	= <<('#REQUIRED' / '#IMPLIED' / '#FIXED' ) #defaultType>>	
	;
	
DefaultValue
	= << String #defaultValue>>
	;


//Name
AttName
	= <<Name #attName>>
	;
ElementName
	= <<Name #elementName>>
	;
MemberName
	= <<Name #memberName>>
	;
EntityName
	= <<Name #entityName>>
	;
DocTypeName
	= <<Name #docTypeName>>
	;

Name
    = << [A-Za-z:] ( '-' / [A-Za-z0-9:._])*  #name >>
    ;

String
    = << '"' ( !'"' . )* '"' #string >>
    ;


//Reference //TODO


Misc
    = Comment / _+
    ;

Comment
    = '<!--' (!'-->' . )* '-->'
    ;
    
ValueSeparator
	= _* ',' _*
	;

_ = [ \t\r\n];