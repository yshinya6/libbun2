TopLevel
    = Document
    ;

Document
    = Misc* DocType Misc* 
    ;
    
DocType
    = '<!DOCTYPE' _* <<DocTypeName@ '[' DTD* #doctype >> ']>'
    ;

DTD
    = _*  '<!' (Element@/Entity@/Attlist@) '>' _*
    ;

Element
	= _* 'ELEMENT' _* <<ElementName@ _*  ( '(' Model (ValueSeparator Model)* ')' (Regex@)? / OtherMember@ ) #element>> _*
	;

Attlist
	= _* 'ATTLIST' _* <<ElementName@ _* (AttParameter@)* #attlist>> _*
	;
	
Entity
	= _* 'ENTITY' _* <<EntityName@ _* String@ #entity>> _*
	;


//for Element
Model
	= _* (Member@) _*
	;
Member
	= << (Data@/MemberName@) (OR@)? (ValueSeparator? (Data@/MemberName@) (OR@)?)* #member>> 
	;
Data
	= << '#PCDATA'  #data>>
	;
Regex
	= << ( '*' / '+' / '?') #regex>>
	;
OR
	= << _* '|'  _* #or>>
	;
OtherMember
	= << ('EMPTY' / 'ANY') #others>>
	;
	
//for Attlist
AttParameter
	= << _* AttName@ _* DataType@ _* Default@ _* #attParameter>>
	; 
DataType
	= << ('CDATA'/'IDREF'/'ID'/'ENTITY'/'ENTITIES'/'NMTOKENS'/'NMTOKEN'/'Enumerated') #dataType>>
	;
Default
	= << DefaultType@  _* (DefaultValue@)* #default>>
	;
DefaultType
	= <<('#REQUIRED' / '#IMPLIED' / '#FIXED' ) #defaultType>>	
	;
DefaultValue
	= << String #defaultValue>>
	;


//Name
AttName
	= <<Name #attName>>
	;
ElementName
	= <<Name #elementName>>
	;
MemberName
	= <<Name@ (Regex@)? #memberName>>
	;
EntityName
	= <<Name #entityName>>
	;
DocTypeName
	= <<Name #docTypeName>>
	;

Name
    = << [A-Za-z:] ( '-' / [A-Za-z0-9:._])*  #name >>
    ;

String
    = << '"' ( !'"' . )* '"' #string >>
    ;


//Reference //TODO


Misc
    = Comment / _+
    ;

Comment
    = '<!--' (!'-->' . )* '-->'
    ;
    
ValueSeparator
	= _* ',' _*
	;

_ = [ \t\r\n];